Program:
  commands*=Command
;

Command:
  IncludeCommand | IfCommand |
  FunctionDefinition | SetCommand | EndCommand |
  ReturnCommand | ImportCommand | PipeCommand
;

IncludeCommand:
  'include' files+=STRING[eolterm]
;

ImportCommand:
  'import' files+=STRING[eolterm]
;

Param:
    key=ID '<=' value=IDorSTRING
;

CustomList:
  '['(components+=IDorSTRING[','])? ']'
;

CustomDict:
  '{' (entries+=DictEntry[','])? '}'
;

DictEntry:
  key=STRING ':' value=IDorSTRING
;

NamedArg:
    key=ID '=' value=IDorSTRING
;

StringData:
  data=/(?ms)\'{3}(.*?)\'{3}/
;

VarReference:
  id=ID('.'childs+=ID['.'])?
;

ListDictReference:
  id=ID '[' member=IDorSTRING ']'
;

ExtParameter:
  '%' param=ID
;

EnvVariable:
  '$' var=ID
;

Source:
  (':' source=ID) | ('*' sourceIndirect=ID)
;

IDorSTRING:
  value=ListDictReference | value=FunctionCall | value=CustomList |
  value=CustomDict | value=StringData | value=VarReference | value=STRING |
  value=INT | value=ExtParameter | value=EnvVariable | value=Source
;

SetLHS:
  ListDictReference | ID
;

SetCommand:
  key=SetLHS '=' value=IDorSTRING
;

FunctionCall:
  name=ID ('()' | '(' (namedArgs+=NamedArg[','] | args+=IDorSTRING[',']) ')') ('{'
      commands*=Command
    '}')?
;

FunctionDefinition:
  'function' name=ID '(' args*=ID[','] ')' ('=>' targetStreams+=ID[','])? '{'
    commands*=Command
  '}'
;

ReturnCommand:
  'return ' (value=IDorSTRING)? | 'return'
;

OutputId:
  source=ID ('(' fields+=ID ')' '=>' target=ID)?
;

IfCommand:
  main=IfBranch elsifs*=ElsifBranch (default=ElseBranch)?
;

IfBranch:
  'if' '(' condition=BoolExpr ')' '{'
    commands+=Command
  '}'
;

ElsifBranch:
'elsif' '(' condition=BoolExpr ')' '{'
  commands+=Command
'}'
;

ElseBranch:
  'else' '{'
    commands+=Command
  '}'
;

BoolExpr:
  cond=IDorSTRING
;

EndCommand:
  'done'
;

Comment:
  /#.*$/
;

PipeCommand:
  left+=IDorSTRING[','] ('=>' right=PipeCommand)?
;
